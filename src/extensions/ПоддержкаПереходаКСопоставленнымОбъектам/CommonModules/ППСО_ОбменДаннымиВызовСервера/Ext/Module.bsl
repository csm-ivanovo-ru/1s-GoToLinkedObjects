#Область ПрограммныйИнтерфейс

// Функция - Получить реквизиты подключения к узлу информационной базы
//   изм регистра НастройкиТранспортаОбменаДанными
//
// Параметры:
//  УзелИнформационнойБазы	 - ПланОбменаСсылка	 - узел информационной базы, для которого
//    необходимо получить реквизиты подключения
// 
// Возвращаемое значение:
//   Структура - структура, содержащая реквизиты подключения к информационной базе
//     (ИмяСервера, ИмяИнформационнойБазы)
//
Функция ПолучитьРеквизитыПодключенияКУзлуИнформационнойБазы(УзелИнформационнойБазы) Экспорт
	Контекст = "ППСО_ОбменДаннымиВызовСервера.ПолучитьРеквизитыПодключенияКУзлуИнформационнойБазы";

	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		Контекст,
		"УзелИнформационнойБазы",
		УзелИнформационнойБазы,
		ПланыОбмена.ТипВсеСсылки());
	
	// Запрос для получения реквизитов подключения к узлу информационной базы
	ЗапросРеквизитовПодключения = Новый Запрос;
	ЗапросРеквизитовПодключения.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиТранспортаОбменаДанными.COMИмяСервера1СПредприятия КАК ИмяСервера,
		|	НастройкиТранспортаОбменаДанными.COMИмяИнформационнойБазыНаСервере1СПредприятия КАК ИмяИнформационнойБазы
		|ИЗ
		|	РегистрСведений.НастройкиТранспортаОбменаДанными КАК НастройкиТранспортаОбменаДанными
		|ГДЕ
		|	НастройкиТранспортаОбменаДанными.Корреспондент = &УзелИнформационнойБазы
		|";
	//

	ЗапросРеквизитовПодключения.УстановитьПараметр("УзелИнформационнойБазы", УзелИнформационнойБазы);
	РезультатЗапроса = ЗапросРеквизитовПодключения.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не обнаружены реквизиты подключения к узлу информационной базы ""%2"".
				|(в регистре НастройкиТранспортаОбмена).
				|Переход к сопоставленному объекту невозможен.
				|
				|Контекст ""%1""'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
			Контекст,
			Строка(УзелИнформационнойБазы.Наименование));
		ЗаписьЖурналаРегистрации("Переход к сопоставленному объекту. Поиск реквизитов подключения к ИБ",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.НайтиПоТипу(ТипЗнч(УзелИнформационнойБазы)), УзелИнформационнойБазы,
			ТекстСообщения);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;

	РеквзитыПодключения = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(РезультатЗапроса.Выгрузить()[0]);
	Возврат РеквзитыПодключения;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
