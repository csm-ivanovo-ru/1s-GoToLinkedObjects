#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Ключ.Пустой() Тогда
		ЗаполнитьДополнительныеРеквизитыИсточника();
	КонецЕсли;

	ПриИзмененииСопоставленногоОбъекта();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПерейтиКСопоставленномуОбъекту(Команда)
	ПерейтиПоНавигационнойСсылке(НавигационнаяСсылкаПриемника);
КонецПроцедуры

&НаКлиенте
Процедура УникальныйИдентификаторИсточникаПриИзменении(Элемент)
	ЗаполнитьДополнительныеРеквизитыИсточника();
КонецПроцедуры

&НаКлиенте
Процедура УзелИнформационнойБазыПриИзменении(Элемент)
	ПриИзмененииСопоставленногоОбъекта();
КонецПроцедуры

&НаКлиенте
Процедура УникальныйИдентификаторПриемникаПриИзменении(Элемент)
	ПриИзмененииСопоставленногоОбъекта();
КонецПроцедуры

&НаКлиенте
Процедура ТипПриемникаПриИзменении(Элемент)
	ПриИзмененииСопоставленногоОбъекта();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьИзНавигационнойСсылкиТекущегоУзла(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаНавигационнойСсылкиТекущегоУзла", ЭтотОбъект);
	ПоказатьВводСтроки(Оповещение, , "Введите навигационную ссылку для указания объекта", 0, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаНавигационнойСсылкиТекущегоУзла(Результат, Параметры) Экспорт
 
	Если Не Результат = Неопределено Тогда
		Попытка
			Запись.УникальныйИдентификаторИсточника =
				РаботаСНавигационнымиСсылкамиВызовСервера.ПолучитьСсылкуПоНавигационнойСсылке(Результат);
		Исключение
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Ожидалась навигационная ссылка на объект в текущем узле информационной базы'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), ,
				"УникальныйИдентификаторИсточника", "Запись");
								 
			Запись.УникальныйИдентификаторИсточника = Неопределено;
		КонецПопытки;
		ЗаполнитьДополнительныеРеквизитыИсточника();
    КонецЕсли;
 
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзНавигационнойСсылкиПриемника(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаНавигационнойСсылкиПриемника", ЭтотОбъект);
	ПоказатьВводСтроки(Оповещение, , "Введите навигационную ссылку из базы - приёмника для указания сопоставленного объекта", 0, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаНавигационнойСсылкиПриемника(Результат, Параметры) Экспорт
 
	Если Не Результат = Неопределено Тогда
		Попытка
			РеквизитыНавигационнойСсылки = РаботаСНавигационнымиСсылкамиКлиентСервер.ПолучитьРеквизитыНавигационнойСсылки(Результат);
			Запись.УникальныйИдентификаторПриемника = РеквизитыНавигационнойСсылки.УникальныйИдентификатор;
			Запись.ТипПриемника = СтрЗаменить(РеквизитыНавигационнойСсылки.ИмяТипа, ".", "Ссылка.");
		Исключение
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Ожидалась навигационная ссылка на сопоставленный объект в информационной базе - приёмнике'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), ,
				"УникальныйИдентификаторПриемника", "Запись");

			Запись.УникальныйИдентификаторПриемника = "";
			Запись.ТипПриемника = "";
		КонецПопытки;
		ПриИзмененииСопоставленногоОбъекта();
    КонецЕсли;
 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДополнительныеРеквизитыИсточника()
	Если ЗначениеЗаполнено(Запись.УникальныйИдентификаторИсточника) Тогда 
		Запись.УникальныйИдентификаторИсточникаСтрокой = Запись.УникальныйИдентификаторИсточника.УникальныйИдентификатор();
		Запись.ТипИсточника = ПолучитьСтроковоеПредставлениеТипа(ТипЗнч(Запись.УникальныйИдентификаторИсточника));
	Иначе
		Запись.УникальныйИдентификаторИсточникаСтрокой = "";
		Запись.ТипИсточника = "";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииСопоставленногоОбъекта()
	
	ДоступностьПерехода = Ложь;
	
	//Попытка

		Если ЗначениеЗаполнено(Запись.УзелИнформационнойБазы)
			И ЗначениеЗаполнено(Запись.УникальныйИдентификаторПриемника)
			И ЗначениеЗаполнено(Запись.ТипПриемника) Тогда 

			НавигационнаяСсылкаИнформационнойБазыПриемника =
				ППСО_ОбменДаннымиВызовСервера.ПолучитьНавигационнуюСсылкуУзлаИнформационнойБазы(Запись.УзелИнформационнойБазы);

			ЭтаФорма.НавигационнаяСсылкаПриемника = РаботаСНавигационнымиСсылкамиКлиентСервер.ПолучитьНавигационнуюСсылкуПоРеквизитам(
				Запись.УникальныйИдентификаторПриемника, Запись.ТипПриемника,
				НавигационнаяСсылкаИнформационнойБазыПриемника);
				
			ДоступностьПерехода = Истина;
			
		Иначе
			ЭтаФорма.НавигационнаяСсылкаПриемника = "";
		КонецЕсли;

	//Исключение
	//КонецПопытки;

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПерейтиКСопоставленномуОбъекту", "Доступность", ДоступностьПерехода);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаПерейтиКСопоставленномуОбъекту", "Доступность", ДоступностьПерехода);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтроковоеПредставлениеТипа(Знач ТипЗначения)
	Возврат ОбщегоНазначения.СтроковоеПредставлениеТипа(ТипЗначения);
КонецФункции

#КонецОбласти
